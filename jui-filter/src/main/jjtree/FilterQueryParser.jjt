options {
  LOOKAHEAD = 2;
  STATIC = false;
  VISITOR = false;
  NODE_USES_PARSER = true;
  JAVA_UNICODE_ESCAPE = true;
  JAVA_TEMPLATE_TYPE = "modern";
  MULTI = true;
  NODE_PREFIX = "Ast";
  NODE_CLASS = "BaseNode";
}

PARSER_BEGIN(FilterQueryParser)

package com.effacy.jui.filter.parser;

import com.effacy.jui.filter.builder.IExpressionBuildable;
import com.effacy.jui.filter.builder.IExpressionBuilder;
import com.effacy.jui.filter.parser.BaseNode.ValueType;

public class FilterQueryParser {

    public static ParsedExpression parse(String content) throws FilterQueryParserException {
        try {
            return new ParsedExpression (new FilterQueryParser(content).parse());
        } catch (java.lang.Throwable e) {
            throw new FilterQueryParserException(e.getMessage ());
        }
    }

    public static class ParsedExpression implements IExpressionBuildable<String> {
        protected AstRoot root;
        ParsedExpression(AstRoot root) {
            this.root = root;
        }
        public <T> T build(IExpressionBuilder<T,String> builder) {
            return root.build(builder);
        }
        public void print() {
            root.print(0);
        }
    }

    public static class FilterQueryParserException extends Exception {
        public FilterQueryParserException(String message) {
            super (message);
        }
    }
}

PARSER_END(FilterQueryParser)

SKIP : { " " | "\t" | "\n" | "\r" }

// Logical Operators
TOKEN : { < AND : "AND" | "and" > }
TOKEN : { < OR : "OR" | "or" > }
TOKEN : { < NOT : "NOT" | "not" | "!" | "~" > }

// Comparison Operators
TOKEN : { < EQ : "=" | "==" | "IS" | "is" > }
TOKEN : { < NEQ : "!=" | "~=" | "<>" > }
TOKEN : { < GT : ">" > }
TOKEN : { < GTE : ">=" > }
TOKEN : { < LT : "<" > }
TOKEN : { < LTE : "<=" > }
TOKEN : { < IN : "IN" | "in" > }
TOKEN : { < NOT_IN : "NOT IN" | "not in" > }
TOKEN : { < CONTAINS : "CONTAINS" | "contains" > }
TOKEN : { < STARTS_WITH : "STARTS WITH" | "starts with" > }
TOKEN : { < ENDS_WITH : "ENDS WITH" | "ends with" > }

// Parentheses
TOKEN : { < LPAREN : "(" > }
TOKEN : { < RPAREN : ")" > }

// Brackets and separators
TOKEN : { < LBRACKET : "[" > }
TOKEN : { < RBRACKET : "]" > }
TOKEN : { < COMMA : "," > }

// Literals
TOKEN : { < STRING : "\"" (~["\""])* "\"" > }
TOKEN : { < TRUE : "true" > }
TOKEN : { < FALSE : "false" > }
TOKEN : { < INTEGER : ("-" )? ("0" | ["1"-"9"]) (["0"-"9"])* > }
TOKEN : { < DECIMAL : ("-" )? ("0" | ["1"-"9"]) (["0"-"9"])* "." (["0"-"9"])+ > }
TOKEN : { < IDENTIFIER : (["A"-"Z", "a"-"z"]) (["A"-"Z", "a"-"z", "0"-"9", "_"])* > }

/** Main production. */
AstRoot parse() #Root : {}
{
  OrExpression() <EOF> { return jjtThis; }
}

void OrExpression() #void : {}
{
  AndExpression()
  (
    <OR> AndExpression() #OrExpression(2)
  )*
}

void AndExpression() #void : {}
{
  NotExpression()
  (
    <AND> NotExpression() #AndExpression(2)
  )*
}

void NotExpression() #void : {}
{
  <NOT> NotExpression() #NotExpression(1)
  | PrimaryExpression()
}

void PrimaryExpression() #void : {}
{
    ComparisonExpression()
  | <LPAREN> OrExpression() <RPAREN>
}

void ComparisonExpression() #void : {}
{
  Field()
  (
    <EQ> SimpleValue() #Equals(2)
    | <NEQ> SimpleValue() #NotEquals(2)
    | <GT> SimpleValue() #GreaterThan(2)
    | <GTE> SimpleValue() #GreaterThanOrEqual(2)
    | <LT> SimpleValue() #LessThan(2)
    | <LTE> SimpleValue() #LessThanOrEqual(2)
    | <IN> ListValue() #In(2)
    | <NOT_IN> ListValue() #NotIn(2)
    | <CONTAINS> SimpleValue() #Contains(2)
    | <STARTS_WITH> SimpleValue() #StartsWith(2)
    | <ENDS_WITH> SimpleValue() #EndsWith(2)
  )
}

void Field() #Field : { Token t; }
{
  ( t = <IDENTIFIER> )
  {
    jjtThis.setImage(t.image);
  }
}

void SimpleValue() #Value : { Token t; }
{
    t = <STRING> { jjtThis.setImage(t.image.substring(1, t.image.length() - 1)); jjtThis.setValueType(ValueType.STRING); }
  | t = <IDENTIFIER> { jjtThis.setImage(t.image); jjtThis.setValueType(ValueType.LITERAL); }
  | t = <INTEGER> { jjtThis.setImage(t.image); jjtThis.setValueType(ValueType.INTEGER); }
  | t = <DECIMAL> { jjtThis.setImage(t.image); jjtThis.setValueType(ValueType.DECIMAL); }
  | t = <TRUE> { jjtThis.setImage("true"); jjtThis.setValueType(ValueType.BOOLEAN); }
  | t = <FALSE> { jjtThis.setImage("false"); jjtThis.setValueType(ValueType.BOOLEAN); }
}

void ListValue() #List : {}
{
    <LBRACKET> SimpleValue() ( <COMMA> SimpleValue() )* <RBRACKET>
}